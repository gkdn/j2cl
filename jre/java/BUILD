# Description:
#   j2cl/jre: JRE subset for transpilation.
#

load("//build_defs:rules.bzl", "j2cl_library", "j2wasm_library")
load("@bazel_tools//tools/jdk:default_java_toolchain.bzl", "default_java_toolchain")
load("@io_bazel_rules_closure//closure:defs.bzl", "closure_js_library")
load(":package_sources.bzl", "package_sources")
load(":jdk_system.bzl", "jdk_system")

package(
    default_visibility = ["//transpiler/javatests:__subpackages__"],
    licenses = ["notice"],  # Apache 2.0
)

JRE_JAVAC_VERSION_OPTS = [
        # Remove the system (aka pre Java9 'bootclasspath').
        # We need this to compile JRE otherwise it will result in a split package for JRE sources.
        "--system",
        "none",
        # Allow JRE classes are allowed to depend on the jsinterop annotations
        "--add-reads",
        "java.base=ALL-UNNAMED",
        # Enable Java 11 for the JRE sources
        "-source",
        "11",
        "-target",
        "11",
]

JRE_JAVAC_OPTS = JRE_JAVAC_VERSION_OPTS + [
    # Avoid log site injection which introduces calls to unsupported APIs
    "-XDinjectLogSites=false",
]

package_sources(
    name = "jre_java_files",
    srcs = [
        "//third_party:gwt_java_emul",
        "//third_party:gwt_java_emul_internal",
    ],
    excludes = glob(["super-wasm/**"]),
    super_srcs = glob(["**/*.java"]),
)

package_sources(
    name = "jre_java_files-j2wasm",
    srcs = [
        "//third_party:gwt_java_emul",
        "//third_party:gwt_java_emul_internal",
    ],
    excludes = [
        "java/util/InternalHashCodeMap.java",  # J2WASM AbstractHashMap doesn't need it.
        "java/util/InternalStringMap.java",  # J2WASM AbstractHashMap doesn't need it.
    ],
    super_srcs = glob(
            ["**/*.java"],
            exclude = glob(["**/super-wasm/*.java"]) + [
                "javaemul/internal/LongUtils.java",  # GWT version actually works for WASM in this case.
            ],
        ),
    super_wasm_srcs = glob(["super-wasm/**/*.java"]),
)

filegroup(
    name = "jre_native_files",
    srcs = glob(
        ["**/*.js"],
        exclude = ["super-wasm/**/*"],
    ) + [
        "//jre/java/javaemul/internal/vmbootstrap/primitives",
    ],
)

j2wasm_library(
    name = "jre-j2wasm",
    srcs = [
        ":jre_java_files-j2wasm",
    ],
    javacopts = JRE_JAVAC_OPTS,
    visibility = ["//build_defs/internal_do_not_use:__pkg__"],
    deps = [
        "//third_party:gwt-javaemul-internal-annotations-j2wasm",
        "//third_party:gwt-jsinterop-annotations-j2wasm",
    ],
)

closure_js_library(
    name = "j2wasm_js",
    srcs = ["super-wasm/j2wasm.js"],
    visibility = ["//:__pkg__"],
)

j2cl_library(
    name = "jre",
    srcs = [
        ":jre_java_files",
        ":jre_native_files",
    ],
    javacopts = JRE_JAVAC_OPTS,
    visibility = [
        "//:__pkg__",
        "//transpiler/javatests:__subpackages__",
    ],
    deps = [
        "//third_party:closure_math_long",
        "//third_party:closure_reflect",
        "//third_party:gwt-javaemul-internal-annotations-j2cl",
        "//third_party:gwt-jsinterop-annotations-j2cl",
    ],
)

# Note that this target is separately compiled as a java_library to avoid circular
# dependency.
java_library(
    name = "jre_bootclasspath",
    srcs = [":jre_java_files"],
    javacopts = JRE_JAVAC_OPTS,
    visibility = ["//transpiler/javatests:__subpackages__"],
    deps = [
        "//third_party:gwt-javaemul-internal-annotations",
        "//third_party:gwt-jsinterop-annotations",
    ],
)

jdk_system(
    name = "jdk_system",
    bootclasspath = ":jre_bootclasspath",
)

JAVA_VERSION = "11"

default_java_toolchain(
    name = "j2cl_java_toolchain",
    bootclasspath = [":jdk_system"],
    source_version = JAVA_VERSION,
    target_version = JAVA_VERSION,
    visibility = ["//build_defs/internal_do_not_use:__pkg__"],
)
